import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

apply plugin: 'com.android.application'
apply plugin: 'jp.leafytree.android-scala'
apply plugin: "androidsvgdrawable"

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFile file('proguard-rules.txt')
        }

        release {
            minifyEnabled true
            proguardFile file('proguard-rules.txt')
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.res.srcDirs = [main.res.srcDirs, "build/generated/res/main"]

        androidTest {
            scala {
                srcDir "tests/src/main" // default: "src/androidTest/scala"
            }
        }
    }
}

dependencies {
    compile project(':core')

    compile "org.scala-lang:scala-library:2.11.5"

    compile "com.actionbarsherlock:actionbarsherlock:4.4.0@aar"
    compile files("./libs/actionbarsherlock-plugin-maps-4.2.0.jar")

    compile "com.android.support:support-v4:19.1.0"

    compile "org.osmdroid:osmdroid-android:3.0.8"
    compile "org.slf4j:slf4j-android:1.6.1-RC1"
    compile "org.ccil.cowan.tagsoup:tagsoup:1.2"

    // maps v1
    // can be found in '/path/to/android/sdk/add-ons/addon-google_apis-google-${compileSdkVersion}/libs/maps.jar'
    compile files('./libs/maps.jar')

    // maps v2
    compile "com.google.android.gms:play-services-maps:7.0.0"

    // analytics
    compile files('./libs/libGoogleAnalyticsV2.jar')
}

/*
Notification:
- 18, 24, 36, 48
- ldpi (120 dpi), mdpi (160 dpi), hdpi (240 dpi), xhdpi (320 dpi)
Launcher:
- 36, 48, 72, 96 (supported in Buketan), and 2 more in the doc - 144, 192
- ldpi (120 dpi), mdpi (160 dpi), hdpi (240 dpi), xhdpi (320 dpi), xxhdpi (480 dpi), xxxhdpi (640 dpi)
*/

// - 32x48 - ldpi or 48x48 mdpi???
// - 128 - hdpi
// - 512 - 128hdpi
task svgToPng(type: SvgDrawableTask) {
    // configuration, see sample project
    from = files('src/main/res-svg')
    // specify the android res folder
    to = file('build/generated/res/main')
    // create qualified directories if missing
    createMissingDirectories = true
    // override files only if necessary
    overwriteMode = 'ifModified'
    // let generate PNG for the following densities only
    // if none specified, all densities will be used
//    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // output format of the generated resources
    outputFormat = 'PNG'
}
